---
layout: post
title: "linux内核自解压＜一＞"
subtitle: linux内核自解压分析
author: "404"
header-style: text
tags:
  - Linux
  - Uboot
---


在内核启动前，bootloader做如下准备工作：
-  CPU寄存器：R0 = 0、R1 = 机器码（linux/arch/tools/mach-types）、R2 = tags在RAM中的物理地址
-  CPU和MMU：SVC模式，禁止中断，MMU关闭，数据Cache关闭。

首先给出内核自解压部分的总流程如下：
![avatar](/img/in-post/Linux/201931501001.png)

程序的入口点在linux/arch/arm/boot/compressed/head.S中，下面来进行详细分析：
```c
    start:  
            .type   start,#function  
            .rept   7  
            mov r0, r0  
            .endr  
       ARM(     mov r0, r0      )  
       ARM(     b   1f      )  
     THUMB(     adr r12, BSYM(1f)   )  
     THUMB(     bx  r12     )  
```
使用.type标号来指明start的符号类型是函数类型，然后重复执行.rept到.endr之间的指令7次，这里一共执行了7次mov r0, r0指令，共占用了4*7 = 28个字节，这是用来存放ARM的异常向量表的。向前跳转到标号为1处执行：
```c
    1:  
            mrs r9, cpsr  
    #ifdef CONFIG_ARM_VIRT_EXT  
            bl  __hyp_stub_install  @ get into SVC mode, reversibly  
    #endif  
            mov r7, r1          @ save architecture ID  
            mov r8, r2          @ save atags pointer  
```
这里将CPU的工作模式保存到r9寄存器中，将uboot通过r1传入的机器码保存到r7寄存器中，将启动参数tags的地址保存到r8寄存器中。
```c
        /* 
         * Booting from Angel - need to enter SVC mode and disable 
         * FIQs/IRQs (numeric definitions from angel arm.h source). 
         * We only do this if we were in user mode on entry. 
         */  
        mrs r2, cpsr        @ get current mode  
        tst r2, #3          @ not user?  
        bne not_angel  
        mov r0, #0x17       @ angel_SWIreason_EnterSVC  
    ARM(        swi 0x123456    )   @ angel_SWI_ARM  
    THUMB(      svc 0xab        )   @ angel_SWI_THUMB  
```
这里将CPU的工作模式保存到r2寄存器中，然后判断是否是SVC模式，如果是USER模式就会通过swi指令产生软中断异常的方式来自动进入SVC模式。
```c
    not_angel:  
            safe_svcmode_maskall r0  
            msr spsr_cxsf, r9       @ Save the CPU boot mode in  
                            @ SPSR  
```
safe_svcmode_maskall是一个宏，定义在arch/arm/include/asm/assembler.h中：
```c
    /* 
     * Helper macro to enter SVC mode cleanly and mask interrupts. reg is 
     * a scratch register for the macro to overwrite. 
     * 
     * This macro is intended for forcing the CPU into SVC mode at boot time. 
     * you cannot return to the original mode. 
     */  
    .macro safe_svcmode_maskall reg:req  
    #if __LINUX_ARM_ARCH__ >= 6  
        mrs \reg , cpsr  
        eor \reg, \reg, #HYP_MODE  
        tst \reg, #MODE_MASK  
        bic \reg , \reg , #MODE_MASK  
        orr \reg , \reg , #PSR_I_BIT | PSR_F_BIT | SVC_MODE  
    THUMB(  orr \reg , \reg , #PSR_T_BIT    )  
        bne 1f  
        orr \reg, \reg, #PSR_A_BIT  
        adr lr, BSYM(2f)  
        msr spsr_cxsf, \reg  
        __MSR_ELR_HYP(14)  
        __ERET  
    1:  msr cpsr_c, \reg  
    2:  
    #else  
    /* 
     * workaround for possibly broken pre-v6 hardware 
     * (akita, Sharp Zaurus C-1000, PXA270-based) 
     */  
        setmode PSR_F_BIT | PSR_I_BIT | SVC_MODE, \reg  
    #endif  
    .endm  
```

这里的注释已经说明了，这里是强制将CPU的工作模式切换到SVC模式，并且关闭IRQ和FIQ中断。然后将r9中保存的原始CPU配置保存到SPSR中。
```c
    #ifdef CONFIG_AUTO_ZRELADDR  
            @ determine final kernel image address  
            mov r4, pc  
            and r4, r4, #0xf8000000  
            add r4, r4, #TEXT_OFFSET  
    #else  
            ldr r4, =zreladdr  
    #endif  
```
内核配置项AUTO_ZRELDDR表示自动计算内核解压地址（Auto calculation of the decompressed kernelimage address），这里没有选择这个配置项，所以保存到r4中的内核解压地址就是zreladdr，这个参数在linux/arch/arm/boot/compressed/Makefile中：
```c
    ifneq ($(CONFIG_AUTO_ZRELADDR),y)  
    LDFLAGS_vmlinux += --defsym zreladdr=$(ZRELADDR)  
    endif  
```

而ZRELADDR定义在arch/arm/boot/Makefile中：
```c
    ifneq ($(MACHINE),)  
    include $(srctree)/$(MACHINE)/Makefile.boot  
    endif  
      
    # Note: the following conditions must always be true:  
    #   ZRELADDR == virt_to_phys(PAGE_OFFSET + TEXT_OFFSET)  
    #   PARAMS_PHYS must be within 4MB of ZRELADDR  
    #   INITRD_PHYS must be in RAM  
    ZRELADDR    := $(zreladdr-y)  
    PARAMS_PHYS := $(params_phys-y)  
    INITRD_PHYS := $(initrd_phys-y)  
```
既然看到了内核解压地址zreladdr，也顺便来看一下params_phys和initrd_phys的值，他们最终由arch/arm/mach-$(SOC)/Makefile.boot决定，我们这里用S3C24XX，他的Makefile.boot内容如下：
```c

ifeq ($(CONFIG_PM_H1940),y)
	zreladdr-y	+= 0x30108000
	params_phys-y	:= 0x30100100
else
	zreladdr-y	+= 0x30008000
	params_phys-y	:= 0x30000100
endif

```
这里的params_phys-y和initrd_phys-y是内核参数的物理地址和initrd文件系统的物理地址。其实除了zreladdr外这些地址uboot都会传入的。
```c
    /* 
     * Set up a page table only if it won't overwrite ourself. 
     * That means r4 < pc && r4 - 16k page directory > &_end. 
     * Given that r4 > &_end is most unfrequent, we add a rough 
     * additional 1MB of room for a possible appended DTB. 
     */  
    mov r0, pc  
    cmp r0, r4  
    ldrcc   r0, LC0+32  
    addcc   r0, r0, pc  
    cmpcc   r4, r0  
    orrcc   r4, r4, #1      @ remember we skipped cache_on  
    blcs    cache_on  
```
这里将比较当前PC地址和内核解压地址，只有在不会自覆盖的情况下才会创建一个页表，如果当前运行地址PC小于解压地址r4，则读取LC0+32地址处的内容加载到r0中，否则跳转到cache_on处执行缓存初始化和MMU初始化。LC0是地址表，定义在525行：
```c
            .align  2  w
            .type   LC0, #object  
    LC0:        .word   LC0         @ r1  
            .word   __bss_start     @ r2  
            .word   _end            @ r3  
            .word   _edata          @ r6  :i
            .word   input_data_end - 4  @ r10 (inflated size location)  
            .word   _got_start      @ r11  
            .word   _got_end        @ ip  
            .word   .L_user_stack_end   @ sp  
            .word   _end - restart + 16384 + 1024*1024  
            .size   LC0, . - LC0  
```
LC0+32地址处的内容为：_end -restart + 16384 + 1024*1024，所指的就是程序长度+16k的页表长+1M的DTB空间。继续比较解压地址r4（0x00008000）和当前运行程序的（结束地址+16384 + 1024*1024），如果小于则不进行缓存初始化并置位r4最低位进行标识。总的来说就是这样：
- PC >= r4：直接进行缓存初始化
- PC < r4 && _end + 16384+ 1024*1024 > r4：不进行缓存初始化
- PC < r4 && _end + 16384+ 1024*1024 <= r4：执行缓存初始化

这里先暂时不分析cache_on（已补充在文中最后分析），继续沿主线往下分析：
```c
    restart:    adr r0, LC0  
            ldmia   r0, {r1, r2, r3, r6, r10, r11, r12}  
            ldr sp, [r0, #28]  
```
通过前面LC0地址表的内容可见，这里r0中的内容就是编译时决定的LC0的实际运行地址（特别注意不是链接地址），然后调用ldmia命令依次将LC0地址表处定义的各个地址加载到r1、r2、r3、r6、r10、r11、r12和SP寄存器中去。执行之后各个寄存器中保存内容的意义如下：
- r0：LC0标签处的运行地址
- r1：LC0标签处的链接地址
- r2：__bss_start处的链接地址
- r3：_ednd处的链接地址（即程序结束位置）
- r6：_edata处的链接地址（即数据段结束位置）
- r10：压缩后内核数据大小位置
- r11：GOT表的启示链接地址
- r12：GOT表的结束链接地址
- sp：栈空间结束地址
195~196行这段代码通过反汇编来看着部分代码会更加清晰（反汇编arch/arm/boot /compressed/vmlinux）：
```c
    000000c0 <restart>:  
          c0:   e28f0e13    add r0, pc, #304    ; 0x130  
          c4:   e8901c4e    ldm r0, {r1, r2, r3, r6, sl, fp, ip}  
          c8:   e590d01c    ldr sp, [r0, #28]  
```
由于实际的运行在物理地址为0x00008000处，所以r0 = pc + 0x130 = 0x00008000 + 0xc0 + 0x8 + 0x130 = 0x00008000 +0x1F8，而0x000081F8物理地址处的内容就是LC0：
```
    000001f8 <LC0>:  
         1f8:   000001f8    strdeq  r0, [r0], -r8  
         1fc:   006f2a80    rsbeq   r2, pc, r0, lsl #21  
         200:   006f2a98    mlseq   pc, r8, sl, r2  ; <UNPREDICTABLE>  
         204:   006f2a80    rsbeq   r2, pc, r0, lsl #21  
         208:   006f2a45    rsbeq   r2, pc, r5, asr #20  
         20c:   006f2a58    rsbeq   r2, pc, r8, asr sl  ; <UNPREDICTABLE>  
         210:   006f2a7c    rsbeq   r2, pc, ip, ror sl  ; <UNPREDICTABLE>  
         214:   006f3a98    mlseq   pc, r8, sl, r3  ; <UNPREDICTABLE>  
         218:   007f69d8    ldrsbteq    r6, [pc], #-152  
         21c:   e320f000    nop {0}  
```
在获取了LC0的链接地址和运行地址后，就可以通过计算这两者之间的差值来获得当前行的地址是否就是编译时的链接地址。

```
    /* 
     * We might be running at a different address.  We need 
     * to fix up various pointers. 
     */  
    sub r0, r0, r1      @ calculate the delta offset  
    add r6, r6, r0      @ _edata  
    add r10, r10, r0        @ inflated kernel size location  
```
将运行地址和链接地址的偏移保存到r0寄存器中，然后更新r6和r10中的地址，将其转换为实际的运行地址。
```
    /* 
     * The kernel build system appends the size of the 
     * decompressed kernel at the end of the compressed data 
     * in little-endian form. 
     */  
    ldrb    r9, [r10, #0]  
    ldrb    lr, [r10, #1]  
    orr r9, r9, lr, lsl #8  
    ldrb    lr, [r10, #2]  
    ldrb    r10, [r10, #3]  
    orr r9, r9, lr, lsl #16  
    orr r9, r9, r10, lsl #24  
```
注释中说明了，内核编译系统在压缩内核时会在末尾处以小端模式附上未压缩的内核大小，这部分代码的作用就是将该值计算出来并保存到r9寄存器中去。
```
    #ifndef CONFIG_ZBOOT_ROM  
            /* malloc space is above the relocated stack (64k max) */  
            add sp, sp, r0  
            add r10, sp, #0x10000  
    #else  
            /* 
             * With ZBOOT_ROM the bss/stack is non relocatable, 
             * but someone could still run this code from RAM, 
             * in which case our reference is _edata. 
             */  
            mov r10, r6  
    #endif  
```
这里将镜像的结束地址保存到r10中去，如果定义了ZBOOT_ROM则bss和stack是非可重定位的，这里将r10设置为sp结束地址上64kb处（这64kB空间是用来作为堆空间的）。
接下来内核如果配置为支持设备树（DTB）会做一些特别的工作，先跳过。
```
    /* 
     * Check to see if we will overwrite ourselves. 
     *   r4  = final kernel address (possibly with LSB set) 
     *   r9  = size of decompressed image 
     *   r10 = end of this image, including  bss/stack/malloc space if non XIP 
     * We basically want: 
     *   r4 - 16k page directory >= r10 -> OK 
     *   r4 + image length <= address of wont_overwrite -> OK 
     * Note: the possible LSB in r4 is harmless here. 
     */  
            add r10, r10, #16384  
            cmp r4, r10  
            bhs wont_overwrite  
            add r10, r4, r9  
            adr r9, wont_overwrite  
            cmp r10, r9  
            bls wont_overwrite  
```
这里r4、r9和r10中的内容见注释。这部分代码用来分析当前代码是否会和最后的解压部分重叠，如果有重叠则需要执行代码搬移。首先比较内核解压地址r4-16Kb（这里是0x00004000，包括16KB的内核页表存放位置）和r10，如果r4 – 16kB >= r10，则无需搬移，否则继续计算解压后的内核末尾地址是否在当前运行地址之前，如果是则同样无需搬移，不然的话就需要进行搬移了。

总结一下可能的3种情况：
- 内核起始地址– 16kB >= 当前镜像结束地址：无需搬移
- 内核结束地址 <= wont_overwrite运行地址：无需搬移
- 内核起始地址– 16kB < 当前镜像结束地址 && 内核结束地址 > wont_overwrite运行地址：需要搬移
