---
layout: post
title: "Arm基础知识——几个地址"
subtitle: 编译地址 运行地址 相对地址
author: "404"
header-style: text
tags:
  - Linux
  - Arm
  - Uboot
---

# 1.几个地址介绍

　　运行地址：也叫链接地址或编译地址，是程序定位的绝对地址，即在编译链接时确定的地址。如果程序中有位置相关指令，那么程序在运行时，程序必须在运行地址上。

　　加载地址：程序加载到Arm系统时的内存地址。

　　运行地址和加载地址可能相等，也可能不等，这是为什么？还是得从前一篇文章的一个图来说起：
![avatar](/img/in-post/Linux/201932501001.png)
　　我们只考虑Nordflash，我们看到启动时候,Nordflash映射在0X00000000，启动时候Uboot映像是放在Nordflash中的，此时装载地址即为0x00000000。

　　我们在来看看Uboot程序的链接地址：
```asm
OUTPUT_FORMAT("elf32-littlearm", "elf32-littlearm", "elf32-littlearm")
/*OUTPUT_FORMAT("elf32-arm", "elf32-arm", "elf32-arm")*/
OUTPUT_ARCH(arm)
ENTRY(_start)
SECTIONS
{
	. = 0x00000000;

	. = ALIGN(4);
	.text      :
	{
	  cpu/arm920t/start.o	(.text)
	  *(.text)
	}

	. = ALIGN(4);
	.rodata : { *(.rodata) }

	. = ALIGN(4);
	.data : { *(.data) }

	. = ALIGN(4);
	.got : { *(.got) }

	. = .;
	__u_boot_cmd_start = .;
	.u_boot_cmd : { *(.u_boot_cmd) }
	__u_boot_cmd_end = .;

	. = ALIGN(4);
	__bss_start = .;
	.bss : { *(.bss) }
	_end = .;
}
```
　　链接脚本中，并没有设置LMA，设置LMA是要通过AT来设置，这个LMA即加载地址。那么VMA设置了么？如下：
```makefile
LDFLAGS += -Bstatic -T $(LDSCRIPT) -Ttext $(TEXT_BASE) $(PLATFORM_LDFLAGS)

TEXT_BASE = 0x33F80000
```
　　这两个分别是在Uboot顶层目录`config.mk`和`board/smdk2410/config.mk`中的。所以程序的链接地址或者叫做运行地址是从`0x33F80000`开始的。

# 2.程序运行

　　既然程序的运行地址和装载地址不同，那么程序如何运行呢TEXT_BASE = 0x33F80000？

　　这就跟PIC有关了，即位置无关指令。在汇编语言中，像bl、b、adr指令都是属于位置无关指令，不管程序装载在哪个位置上，bl、b、adr这些指令都能正确运行。这是因为这三个指令的
地址域是基于PC的相对偏移地址，相当于[pc+offset]。当ARM启动时，无论PC处于哪个位置，都能运行成功。

　　那什么是相对地址呢？以NOR Flash为例，NOR Falsh是映射到bank0上面，SDRAM是映射到bank6上面，uboot和内核最终是在SDRAM上面运行，最开始我们是从Nor Flash的零地址开始往后烧录，uboot中至少有一段代码编译地址和运行地址是不一样的，编译uboot或内核时，都会将编译地址放入到SDRAM中，他们最终都会在SDRAM中执行，刚开始uboot在Nor Flash中运行，运行地址是一个低端地址，是bank0中的一个地址，但编译地址是bank6中的地址，这样就会导致绝对跳转指令执行的失败，所以就引出了相对地址的概念。

　　至少在bank0中uboot这段代码要知道不能用b+编译地址这样的方法去跳转指令，因为这段代码的编译地址和运行地址不一样，那如何去做呢？要去计算这个指令运行的真实地址，计算出来后再做跳转，应该是b+运行地址，不能出现b+编译地址，而是b+运行地址，而运行地址是算出来的。如下：
```c
_TEXT_BASE:
	.word	TEXT_BASE

...
...
...

#ifndef CONFIG_SKIP_RELOCATE_UBOOT
relocate:				/* relocate U-Boot to RAM	    */
	adr	r0, _start		/* r0 <- current position of code   */
	ldr	r1, _TEXT_BASE		/* test if we run from flash or RAM */
	cmp     r0, r1                  /* don't reloc during debug         */
	beq     stack_setup

	ldr	r2, _armboot_start
	ldr	r3, _bss_start
	sub	r2, r3, r2		/* r2 <- size of armboot            */
	add	r2, r0, r2		/* r2 <- source end address         */

copy_loop:
	ldmia	r0!, {r3-r10}		/* copy from source address [r0]    */
	stmia	r1!, {r3-r10}		/* copy to   target address [r1]    */
	cmp	r0, r2			/* until source end addreee [r2]    */
	ble	copy_loop
#endif	/* CONFIG_SKIP_RELOCATE_UBOOT */
```

```c
TEXT_BASE:
	.word	TEXT_BASE

.globl lowlevel_init
lowlevel_init:
	/* memory control configuration */
	/* make r0 relative the current location so that it */
	/* reads SMRDATA out of FLASH rather than memory ! */
	ldr     r0, =SMRDATA
	ldr	r1, _TEXT_BASE
	sub	r0, r0, r1
	ldr	r1, =BWSCON	/* Bus Width Status Controller */
	add     r2, r0, #13*4
0:
	ldr     r3, [r0], #4
	str     r3, [r1], #4
	cmp     r2, r0
	bne     0b

	/* everything is fine now */
	mov	pc, lr

	.ltorg
/* the literal pools origin */

SMRDATA:
    .word (0+(B1_BWSCON<<4)+(B2_BWSCON<<8)+(B3_BWSCON<<12)+(B4_BWSCON<<16)+(B5_BWSCON<<20)+(B6_BWSCON<<24)+(B7_BWSCON<<28))
```
　　在这段代码中，由于链接地址和装载地址不同，所以我们如果想将SMRDATA处的复制拷贝到BWSCON寄存器中，因为此时SDRAM初始化都没完成，所以SDRAM中是没有数据的，而链接地址是直接放入
的SDRAM中的地址，此时我们的运行地址是在0x0开始，所以我们只能找SMRDATA在0x0处的相对位置。此时TEXT_BASE存储的是SDRAM开始位置，即UBOOT映像以后装入SDRAM的起始地址，标号SDRDATA也是
相对于SDRAM的相对地址，所以两个值想减，就是相对地址。又由于此时装载地址是0，所以此相对地址加上0就是在装载处SMRDATA的存储地址，将这个地址的值装入BWSCON即可。
 







　　
