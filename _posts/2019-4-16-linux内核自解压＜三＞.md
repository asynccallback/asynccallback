---
layout: post
title: "linux内核自解压＜三＞"
subtitle: linux内核自解压＜三＞
author: "404"
header-style: text
tags:
  - Linux
  - Uboot
---

　　还是继续分析下自解压过程中MMU设置，在内核启动过程中，MMU设置代码如下：

```c
__setup_mmu:	sub	r3, r4, #16384		@ Page directory size
		bic	r3, r3, #0xff		@ Align the pointer
		bic	r3, r3, #0x3f00
/*
 * Initialise the page tables, turning on the cacheable and bufferable
 * bits for the RAM area only.
 */
		mov	r0, r3
		mov	r9, r0, lsr #18
		mov	r9, r9, lsl #18		@ start of RAM
		add	r10, r9, #0x10000000	@ a reasonable RAM size
		mov	r1, #0x12		@ XN|U + section mapping
		orr	r1, r1, #3 << 10	@ AP=11
		add	r2, r3, #16384
1:		cmp	r1, r9			@ if virt > start of RAM
		cmphs	r10, r1			@   && end of RAM > virt
		bic	r1, r1, #0x1c		@ clear XN|U + C + B
		orrlo	r1, r1, #0x10		@ Set XN|U for non-RAM
		orrhs	r1, r1, r6		@ set RAM section settings
		str	r1, [r0], #4		@ 1:1 mapping
		add	r1, r1, #1048576
		teq	r0, r2
		bne	1b
/*
 * If ever we are running from Flash, then we surely want the cache
 * to be enabled also for our execution instance...  We map 2MB of it
 * so there is no map overlap problem for up to 1 MB compressed kernel.
 * If the execution is in RAM then we would only be duplicating the above.
 */
		orr	r1, r6, #0x04		@ ensure B is set for this
		orr	r1, r1, #3 << 10
		mov	r2, pc
		mov	r2, r2, lsr #20
		orr	r1, r1, r2, lsl #20
		add	r0, r3, r2, lsl #2
		str	r1, [r0], #4
		add	r1, r1, #1048576
		str	r1, [r0]
		mov	pc, lr
ENDPROC(__setup_mmu)
```

# 1. 寄存器
　　此过程中，涉及到几个寄存器的作用及初始值
- $r_0$ 一级页表开始地址，在设置页表项内容时，此值在每个过程后增加4，指向下一个要设置的地址
- $r_1$ 一级页表项要设置的内容，主要涉及AP、XN、C、B，其中$r_1$被初始化为`0xC12`
- $r_2$ 一级页表结束地址
- $r_3$ 一级页表开始地址
- $r_4$ 一级页表结束地址

# 2. 过程分析

## 2.1 寄存器初始化

　　在上面代码表中1~14行，可以算作寄存器的初始化，初始化后值如上寄存器中的内容，主要分析几个点：
1. `sub	r3, r4, #16384`是设置页表大小，`#16384`代表的是页表项大小是16KB，为什么是16KB？因为此时MMU采用段表，段表使用32位地址中高12位作为基地址，代表有$2^{12} = 4K$个段；后20位作为一个段的大小，即一个段大小为$2^{20} = 1M$。既然有4K个段，每一个段用4B大小来设置，具体设置值如下，所以一共大小就为16KB。  
　　![avatar](/img/in-post/Linux/2019041601001.webp)
2. `mov	r1, #0x12`、`orr	r1, r1, #3 << 10`,这两项就是设置上图中的AP、XN、C、B

## 2.2 页表项设置

```c
1:		cmp	r1, r9			@ if virt > start of RAM
		cmphs	r10, r1			@   && end of RAM > virt
		bic	r1, r1, #0x1c		@ clear XN|U + C + B
		orrlo	r1, r1, #0x10		@ Set XN|U for non-RAM
		orrhs	r1, r1, r6		@ set RAM section settings
		str	r1, [r0], #4		@ 1:1 mapping
		add	r1, r1, #1048576
		teq	r0, r2
		bne	1b
```
　　上图内容就是进行页表项内容设置，其分支代码其实可以写成如下：
```c
#ifdef CONFIG_CPU_DCACHE_WRITETHROUGH
#define CB_BITS 0x08
#else
#define CB_BITS 0x0c
#endif

/* case 1:  即0x0000 0000 ~ 0x3000 0000,r1 =0xC12
1:		cmp	r1, r9			@  virt < start of RAM
		bic	r1, r1, #0x1c		@ clear XN|U + C + B
		orrlo	r1, r1, #0x10		@ Set XN|U for non-RAM

/* case 2: 即0x3000 0000 ~ 0x4000 0000 即256M大小,r1 =0xC02
1:		cmp	r1, r9			@ if virt > start of RAM
		cmphs	r10, r1			@   && end of RAM > virt
		bic	r1, r1, #0x1c		@ clear XN|U + C + B
		orrhs	r1, r1, r6		@ set RAM section settings

/* case 3: 即0x4000 0000 ~ 0xFFFF FFFF，r1 =0xC12
1:		cmp	r1, r9			@ if virt > start of RAM
		cmphs	r10, r1			@   && end of RAM < virt
		bic	r1, r1, #0x1c		@ clear XN|U + C + B
		orrlo	r1, r1, #0x10		@ Set XN|U for non-RAM
```
　　
